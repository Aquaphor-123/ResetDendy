/*
Подпрограмма сброса.
Использует регистр temp.
temp обнуляет.

1) Отключаем прервание INT0
2) Настраиваем вывод RESET микроконтроллера на выход (на выводе появится низкий уровень - Dendy сброшена).
3) Запускаем подпрограмму задержки на 0.5 с. (Использует Watchdog)
4) Настраиваем вывод RESET микроконтроллера на вход, для выхода из сброса Dendy (так сделано, чтобы не повредить вывод микроконтроллера, если сброс будет произведен кнопкой на приставке).
5) Сбрасываем флаг, флаг необходим для проверки включилась ли Dendy после сброса (флаг устанавливается в векторе обработки прерывания Watchdog).
6) Сбрасываем Watchdog.
7) Запускаем и настраиваем Watchdog на прерывание (прерывание через 2 с).
8) Проверяем появление высоко уровня на сигнале LATCH и установку флага.
	8.1) Если флаг установился раньше сигнала LATCH переходим на шаг 2.
	8.2) Если сигнал поднялся раньше выходим из проверки.
9) Останавливаем Watchdog.
10) Настраиваем и включаем прерывание INT0 по фронту (переход с 0 в 1).
11) Выход.   
*/
resetDendy:

	clr	temp
	out	EIMSK,	temp

	sei

reReset:

	sbi	DDRB,	RESET

	rcall	delay

	cbi	DDRB,	RESET

	clr	flag

	wdr
	ldi	temp,	(1<<WDIE) | (1<<WDP2) | (1<<WDP1) | (1<<WDP0)
	out	WDTCSR,	temp

LATCHLow:
	cpi	flag,	0xff
	breq	reReset

	sbis	PINB,	LATCH
	rjmp	LATCHLow
LATCHHigh:	
	cli

	clr	temp
	out	WDTCSR,	temp

	ldi	temp,	(1<<INT0) 
	out	EIMSK,	temp
	ldi	temp,	(1<<INTF0)
	out	EIFR,	temp

	sei	

	ret
