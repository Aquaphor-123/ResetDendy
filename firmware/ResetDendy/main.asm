/*
Всем привет.
Данная программа была написана под микроконтроллер ATTiny10(4, 5, 9).
Программа выполняет сброс Dendy при помощи комбинации кнопок на джойстике.
Для работы программы необходимо перевести вывод PB3 в режим I/O, для этого нужно запрограммировать Fuse bit RSTDISBL.
Программа проверена на приставке с NTSC чипами, с PAL чипами не тестировалась.
///////////////////////////////////////////////////////////////////////////////
*/
//Программа по умолчанию для приставок, где сброс осуществляется кнопкой, притягивающей вывод res микросхемы PPU к земле питания. 
//Если у Вас NES с CIC (3193a) чипом, то сброс осуществляется кнопкой, притягивающей вывод 7 CIC к плюсу питания. Я не тестировал данный режим, но если вы считаете что понимаете, 
//что делаете, то расскоментируйте строку ниже и пробуйте на NES с CIC.   
//.equ	NES	=	1


//Если хотите посмотреть в симуляторе, раскомментируйте строку ниже.
//Это ускорит прохождение чтения данных и отключит задержку сброса.

//.equ	SIMULATOR	=	1

// Выбор микроконтроллера:
.equ	ATtiny10	=	1
//.equ	ATtiny9	=	1
//.equ	ATtiny5	=	1
//.equ	ATtiny4	=	1

/*
Назначение выводов микроконтроллера можно переназначить:
1) Сигнал LATCH должен быть подключен к выводу PINB2, так как он используется для выхода микроконтроллера из режима сна по прерыванию.
2) Сигнал RESET возможно переподключить с вывода PINB1 на PINB0. 
3) Сигнал DATA возможно переподключить с вывода PINB0 на PINB1 или PINB3.
4) Сигнал CLOCK возможно переподключить с вывода PINB3 на PINB0.

Для перенезазначения выводов измените значения ниже.
*/

.equ	DATA =	PINB0
.equ	RESET =	PINB1
.equ	LATCH =	PINB2
.equ	CLOCK =	PINB3  

//Маски кнопок джойстика
.equ	RIGHT =	0b11111110
.equ	LEFT =	0b11111101
.equ	DOWN =	0b11111011
.equ	UP =	0b11110111
.equ	START =	0b11101111
.equ	SELECT =	0b11011111
.equ	B =	0b10111111
.equ	A =	0b01111111

.equ	TOTAL_BITS =	8

/*
Ниже задаём комбинацию кнопок джойстика для сброса Dendy (проверено только START & SELEC).
Пример:
1. START & SELECT
2. START & SELECT & B
3. START & SELECT & UP
*/

.set	BUTTON_MASK = START & SELECT

/*
Ниже можно указать желаемое количество считываний комбинации кнопок до перехода к сбросу.
Можно добиться парирования ошибочных нажатий.
*/
.set	REPEAT = 5	//255 макс

/*
Сколько секунд ждать восстановления работы приставки до повторной перезагрузки.
*/
.set	REPTIME = 10	//255 макс

//Параметры разгона частоты 
.equ	stepOverClock = 4
.equ	stepOSCCAL = 20
.equ	delayStabil = 50

.ifdef	SIMULATOR
.set	BUTTON_MASK = 0xff
.set	REPEAT = 1
.set	REPTIME = 2	
.warning "Only for the simulator!" 
.endif

.def	temp	=	r16
.def	dataJoy	=	r17
.def	tik	=	r18
.def	RepCount	=	r19

.CSEG
.org	0x0000
rjmp	preinit

.org	INT0addr
reti

.org	PCI0addr
rjmp	PCI0	

.org	WDTaddr
rjmp	WDT
//Обработчик прерывания PCINT0 (Сохраняем SREG, вдруг прерывание пришло в момент сравнения переменных), увеличиваем tik если CLOCK в 0.
PCI0:
in	temp,	SREG
sbis	PINB,	CLOCK
inc	tik
out	SREG,	temp
reti
//Обработчик прерывания WDT (Сохраняем SREG, вдруг прерывание пришло в момент сравнения переменных), увеличиваем repCount.
WDT:
in	temp,	SREG
inc	repCount
out	SREG,	temp
reti

.include	"init.inc" 
.include	"read.inc"
.include	"reset.inc"	
.include	"delay.inc"
/*
Основной цикл:
1) Очищаем счётчик повторов комбинации кнопок.
2) Переводим микроконтроллер в режим сна.
3) Выход из режима сна происходит по прерыванию INT0 (по фронту) от сигнала LATCH (после сигнала LATCH есть 6.3 мкс для того, чтобы считать первый бит посылки, но не ранее, чем 250 нс).
4) Запускаем подпрограмму получения данных от джойстика.
5) Сверяем данные с маской. 
	5.1) Если не совпало переходим к шагу 1 (сбрасываем счётчик повторов, потому что не было необходимого количества повторов).
	5.2) Если совпало, увеличиваем количество повторов комбинации кнопок.
6) Проверяем получены ли все повторы комбинации кнопок.
	6.1) Если нет, переходим к шагу 2 (счётчик повторов не сбрасываем, ждём будет ли ещё повтор комбинации кнопок).
	6.2) Если все получены выполняем сброс Dendy.
*/

main:
	clr	RepCount
loop:
	sleep
	
	rcall	readDataJoy

	cpi	dataJoy,	BUTTON_MASK
	brne	main

	inc	RepCount

	cpi	RepCount,	REPEAT
	brne	loop

	rcall	resetDendy

	rjmp	main	
